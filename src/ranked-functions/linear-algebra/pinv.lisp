(numericals.common:compiler-in-package numericals.common:*compiler-package*)

(5am:in-suite nu::array)

(define-polymorphic-function la:pinv (array-like &key out) :overwrite t
  :documentation "Calculate the psuedo inverse of 2D matrices given by ARRAY-LIKE")

(defun out-shape-compatible-for-pinv-p (a out)
  (declare (type simple-array a out)
           (optimize speed))
  (let ((rank (nu:array-rank a)))
    (and (= rank (nu:array-rank out))
         (if (< rank 2)
             nil
             (and (loop :for d1 :of-type (integer 0 #.array-dimension-limit)
                          :in (narray-dimensions a)
                        :for d2 :of-type (integer 0 #.array-dimension-limit)
                          :in (narray-dimensions out)
                        :repeat (- rank 2)
                        :always (= d1 d2))
                  (= (array-dimension a (- rank 1))
                     (array-dimension out (- rank 2)))
                  (= (array-dimension out (- rank 1))
                     (array-dimension a (- rank 2))))))))

(defpolymorph la:pinv ((a (simple-array <type>)) &key ((out (simple-array <type>))))
    (simple-array <type>)
  (policy-cond:with-expectations (= 0 safety)
      ((assertion (out-shape-compatible-for-pinv-p a out)
                  ()
                  "Cannot compute psuedo inverse of array of shape ~A~%into array of shape ~A."
                  (narray-dimensions a) (narray-dimensions out)))
    (let* ((c-size (c-size <type>))
           (a-layout (eigen-array-layout a))
           (o-layout (eigen-array-layout out))
           (rank   (nu:array-rank a))
           (m (nu:array-dimension a (- rank 2)))
           (n (nu:array-dimension a (- rank 1)))
           (c-name (c-name <type> 'la:pinv)))
      (flet ((pinv (a-size a-ptr a-dims o-ptr o-dims)
               (declare (ignore a-size a-dims o-dims))
               (inline-or-funcall c-name m n a-ptr a-layout o-ptr o-layout)))
        (with-simple-array-broadcast (pinv 2 2) (a c-size) (out c-size)))
      out)))

(defun out-shape-for-pinv (a)
  (declare (type simple-array a)
           (optimize speed))
  (let ((rank (nu:array-rank a)))
    (if (< rank 2)
        (error "Computing psuedo inverse needs a matrix of rank at least 2~%but a matrix of shape ~A with rank ~D was supplied" (narray-dimensions a) rank)
        (nconc (subseq (narray-dimensions a) 0 (- rank 2))
               (list (array-dimension a (- rank 1))
                     (array-dimension a (- rank 2)))))))

(defpolymorph (la:pinv :inline t) ((a (simple-array <type>)) &key ((out null)))
    (or number (simple-array <type>))
  (declare (ignore out))
  (pflet ((out (nu:empty (out-shape-for-pinv a) :type <type>)))
    (declare (type (simple-array <type>) out))
    (la:pinv a :out out)))

(defpolymorph (la:pinv :inline t) ((a list) &key ((out null)))
    (or number simple-array)
  (declare (ignore out))
  (la:pinv (nu:asarray a :type nu:*default-float-format*)))

(defpolymorph (la:pinv :inline t) ((a list) &key ((out (simple-array <type>))))
    (simple-array <type>)
  (la:pinv (nu:asarray a :type <type>) :out out))
