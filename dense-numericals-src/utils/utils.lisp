(in-package :dense-numericals/utils/impl)

(deftype uint32 () `(unsigned-byte 32))
(deftype int64 () '(signed-byte 64))
(deftype int16 () '(signed-byte 16))
(deftype int8  () '(signed-byte 08))

(defvar *default-float-format* 'single-float
  "Used for converting non-float arrays to float arrays for floating-point
operations like trigonometric functions.")
(declaim (type (member single-float double-float) *default-float-format*))

(defvar *broadcast-automatically* t
  "If non-NIL, operations automatically perform broadcasting as necessary.
If NIL, broadcasting is expected to be performed by the user. Such strictness
can be helpful to locate bugs.")

(defparameter *inline-with-multithreading* nil
  "Inlining is usually necessary for smaller arrays; for such arrays multithreading
becomes unnecessary. If this parameter is non-NIL, code using multithreading
would be emitted; otherwise, the code would be skipped.

This is only relevant for transcendental functions which uses lparallel for multithreading.")

(defparameter *multithreaded-threshold* 80000)
(declaim (type fixnum *multithreaded-threshold*))

(declaim (inline cl-array-offset))
(declaim (ftype (function (cl:array) size) cl-array-offset))
(defun cl-array-offset (array)
  (declare (optimize speed)
           (type cl:array array))
  (loop :with total-offset :of-type (signed-byte 61) := 0
        :if (typep array 'cl:simple-array)
          :do (return total-offset)
        :else
          :do (multiple-value-bind (displaced-to offset)
                  (cl:array-displacement array)
                (declare (type (signed-byte 61) offset))
                (incf total-offset offset)
                (setq array displaced-to))))

(define-condition runtime-array-allocation (suboptimal-polymorph-note)
  ()
  (:report (lambda (c s)
             (declare (ignore c))
             (format s "Unable to avoid array allocation at run time. Consider supplying
the OUT argument, and/or ensuring all the appropriate arguments are
arrays of appropriate types."))))

(define-condition incompatible-broadcast-dimensions (error)
  ((dimensions :initarg :dimensions :reader condition-dimensions)
   (array-likes :initarg :array-likes :reader condition-array-likes))
  (:report (lambda (c s)
             (pprint-logical-block (s nil)
               (format s "The following array-likes with dimensions誉ャ犷铒忮怛镝溷狍麸珏翳弪湖アㄣ镱溟糸镱溟礤铙轱铙悌痧蜷铘祜玳汜飙忪镢铋吼弪扉铄痱彐轼ㄦ矧磲英ㄣ镱溟糸镱狎蜥扉脲悌┅┅┅ㄤ彐磲泸溴骢瞠钺礤灬礅溽扉篝怙澌怙澌啜弼犰麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅钺礤灬礅溽扉篝棱镤┅ㄤ彐磲泸孱篚蝈蝻鳝磲觑颦灬秕ī啜狍箦螋ㄥ灬秕候秣磲觑颟灬秕舂⒚毯烈伊汜镱禊栳鲥阂献土氏灬秕舢渝呐斡怒烈伊儆毫乙临矧土巧锰涸盼酉骘狎蜥鏖翳好咸胀苇土氏矧雉桢灬秕趔┅ㄤ彐躅箬狃ㄡ蝌狴扉脲镳糸镱犰ㄡ轶铋狲轶皓换澡轶轶痫翦铘獒祆滹磲轭箴邈殒殂骢钽糸镱犰轸箝钽换翳弪屮轶趔翳犴忾珲轸镦麒狒箬秕熹镱滹鏖翳篝蜷铉戾è溟礤铙轱铙豉疱汜箦狎蜥扉脲箦聃孱沐ㄣ镱戾铉翳狎蜥扉脲箬狃ㄥ祠狎蜥扉脲癌┅ㄡ蝌狴ㄡ蝌狴溟礤铙轱铙狎蜥扉脲┅铋飑┅ㄩ狲轶ㄥ祠溟礤铙轱铙狲轶溟礤铙轱铙┅ㄤ彐轭瀛痫禊盹蝠栝悱骢钽糸镱秕舡箬狃瀛泔眇狒殁戾ㄦ躅泗轱瞽钺礤蝈篝狎珞猴鲥蝼蜷翦舂ㄤ彐轭瀛痫禊盹蝠栝悱骢钽糸镱秕舡箬狃ㄦ躅泗轱瞽钺礤蝈篝狎珞猴鲥蝼蜷翦舂ㄤ邈灬轫ㄩ铎轭孱篚蝈狎蜥┅ㄤ彐躅孱篚蝈狎蜥ㄥ戾礤铘镳糸镱犰箬狃ㄥ戾礤铘豉疱溴驷蹯舡屐屙孱舡豉疱┅磲脲狎蜥箬狃哄戾礤铘豉疱屐屙孱舡豉疱洪铋糸犰屐屙孱ㄣ镥蜚屐屙孱屐屙孱舡豉疱┅