(in-package :dense-numericals/linalg)

(5am:in-suite :dense-numericals)

(define-polymorphic-function det (array-like &key out) :overwrite t
  :documentation "Calculate the determinant of 2D matrices given by ARRAY-LIKE")

(defpolymorph det ((a (simple-array <type> 2)) &key ((out null)))
    number
  (declare (ignore out))
  (let* ((layout (eigen-array-layout a))
         (m (array-dimension a 0))
         (n (array-dimension a 1))
         (c-name (c-name <type> 'det)))
    (declare (ignorable m n))
    (policy-cond:with-expectations (= 0 safety)
        ((assertion (cl:= m n) ()
                    "Computing determinant needs a square matrix~%but a matrix of shape ~A was supplied"
                    (narray-dimensions a)))
      (pflet ((sv (array-storage a)))
        (declare (type (cl:simple-array <type> 1) sv))
        (with-pointers-to-vectors-data ((ptr sv))
          (inline-or-funcall c-name m ptr layout))))))

(defun out-shape-compatible-for-det-p (a out)
  (declare (type simple-array a out)
           (optimize speed))
  (let ((rank (array-rank a)))
    (and (cl:= (- rank 2) (array-rank out))
         (if (< rank 2)
             nil
             (and (loop :for d1 :of-type (integer 0 #.array-dimension-limit)
                          :in (narray-dimensions a)
                        :for d2 :of-type (integer 0 #.array-dimension-limit)
                          :in (narray-dimensions out)
                        :repeat (- rank 2)
                        :always (cl:= d1 d2))
                  (cl:= (array-dimension a (- rank 1))
                        (array-dimension a (- rank 2))))))))

(defpolymorph det ((a (simple-array <type>)) &key ((out (simple-array <type>))))
    (simple-array <type>)
  (policy-cond:with-expectations (cl:= 0 safety)
      ((assertion (out-shape-compatible-for-det-p a out)
                  ()
                  "Cannot compute determinant of array of shape ~A~%into array of shape ~A."
                  (narray-dimensions a) (narray-dimensions out)))
    (let* ((c-size (c-size <type>))
           (a-layout (eigen-array-layout a))
           (rank   (array-rank a))
           (m (array-dimension a (- rank 2)))
           (c-name (c-name <type> 'det))
           (c-type (c-type <type>)))
      (flet ((solve (a-size a-ptr a-dims o-ptr o-dims)
               (declare (ignore a-size a-dims o-dims))
               (inline-or-funcall #'(setf fref)
                                  (inline-or-funcall c-name m a-ptr a-layout)
                                  o-ptr c-type)))
        (with-simple-array-broadcast (solve 2 0) (a c-size) (out c-size)))
      out)))

(defpolymorph out-shape ((name (eql det)) a) list
  (declare (type simple-array a)
           (optimize speed))
  (let ((rank (array-rank a)))
    (if (< rank 2)
        (error "Computing determinant needs an array of at least two dimensions")
        (subseq (narray-dimensions a) 0 (- rank 2)))))

(defpolymorph (det :inline t) ((a (simple-array <type>)) &key ((out null)))
    (or number (simple-array <type>))
  (declare (ignore out))
  (if (cl:= 2 (array-rank a))
      (pflet ((a a))
        (declare (type (simple-array <type> 2) a))
        (det a))
      (pflet ((out (empty (out-shape 'det a) :type <type>)))
        (declare (type (simple-array <type>) out))
        (det a :out out))))

(defpolymorph (det :inline t) ((a list) &key ((out null)))
    (or number simple-array)
  (declare (ignore out))
  (det (asarray a :type *default-float-format*)))

(defpolymorph (det :inline t) ((a list) &key ((out (simple-array <type>))))
    (simple-array <type>)
  (det (asarray a :type <type>) :out out))
