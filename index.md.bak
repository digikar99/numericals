# Numerical Computing in Common Lisp

> Currently, `numericals` and `dense-numericals` provides fast arithmetic, comparison, logical, transcendental, and float-rounding operations. While it is possible to use linear algebra routines by [interfacing with magicl](#interfacing-with-magicl), unless there is sufficient interest, given (i) the limited developer time (ii) the lack of fast basic mathematic libraries in Common Lisp*, `numericals` and `dense-numericals` does not aim to provide fast linear algebra operations at the moment. Improving the interface would be at a higher priority.
>
>*There are a fair number of maintained and unmaintained numerical/scientific computing libraries providing (incomplete) linear algebra routines in Common Lisp - [magicl](https://github.com/quil-lang/magicl), [gsll](https://gsll.common-lisp.dev/), [lisp-matrix](https://github.com/blindglobe/lisp-matrix) [lisp-stat/numerical-utilities](https://github.com/Lisp-Stat/numerical-utilities) (see [awesome-cl](https://github.com/CodyReichert/awesome-cl#numerical-and-scientific)) - yet none has bothered (or needed?) SIMD based fast basic mathematical operations. This library therefore does not wish to focus on linear algebra before getting the fast basic mathematical operations get suitably polished.

## Getting started

Click one of the below links depending on your (i) proficiency in programming (ii) proficiency in Common Lisp (iii) the task at hand.

- I want to get things done reasonably fast. I don't care about maximizing runtime performance so long as it isn't abysmal.
  - I want to operate on CLHS provided arrays. They are good enough for me. I don't care about your "new fancy XYZ arrays".
  - I'm curious about these new fancy arrays. I myself have a fair share of problems with CLHS provided arrays, and certainly wish they could be better.

- I want to extract the maximum juice out of my machine. I know why I need it. I care about every last bit of performance.
  - I'm happy with CLHS provided arrays, OR I wish we had a better alternative to CLHS arrays.
  - I'm a reasonably proficient lisper and have a basic idea about maximizing performance, OR I'm new to lisp, but I have a basic idea about maximizing performance, OR I'm new to lisp, and this will be amongst my first times learning to optimize performance.


## Design Decisions

- Why not GSL? GSL uses its own vector and matrix wrappers, and while one can certainly make CFFI use them, it doesn't seem like a good way. Using BLAS and LAPACK looks better.


## Getting started

If you are happy with Common Lisp arrays, you can `(ql:quickload "numericals")`. Otherwise if you think arrays could look nicer, and think numpy's way of doing things are sensible*, then you can `(ql:quickload "dense-numericals")`.

```lisp
(uiop:define-package :numericals-demo
  ;; (:mix :numericals :cl))
  (:mix :dense-numericals :dense-arrays-plus-lite :cl))
```

### An introduction to dense-arrays

The arrays that `dense-numericals` works on are provided by [dense-arrays](https://github.com/digikar99/dense-arrays/). At its simplest, these are wrappers around `cl:vector` and enable numpy-like* copy-free slicing aka copy-free selection of array elements / axes. 

```lisp
CL-USER> (setq cl:*print-length* 10)
CL-USER> (let ((a (cl:make-array '(1000 1000))))
           (select:select a t 0)) ; select the 0th element of every row
#(0 0 0 0 0 0 0 0 0 0 ...)
CL-USER> (let ((a (dense-arrays:make-array '(1000 1000))))
           (dense-arrays:aref a nil 0)) ; select the 0th element of every row
#<STANDARD-DENSE-ARRAY NIL 1000 T
  0
  0
  0
  0
  0
  0
  0
  0
  0
  0
  ...
 {10511DAA23}>
CL-USER> (let ((a (cl:make-array '(1000 1000))))
           (time (loop for i below 1000 do (select:select a t i))))
Evaluation took:
  0.159 seconds of real time
  0.159541 seconds of total run time (0.159541 user, 0.000000 system)
  100.63% CPU
  352,387,000 processor cycles
  10,863,248 bytes consed

NIL
CL-USER> (let ((a (dense-arrays:make-array '(1000 1000))))
           (time (loop for i below 1000 do (dense-arrays:aref a nil i))))
Evaluation took:
  0.000 seconds of real time
  0.000910 seconds of total run time (0.000878 user, 0.000032 system)
  100.00% CPU
  2,001,298 processor cycles
  261,904 bytes consed

NIL
```

Here, the [select:select](https://lisp-stat.github.io/select/#index-Generic-Function_002c-select) operation copies over the elements of the array into the new array. While the `dense-arrays:aref` merely returns another wrapper over a certain section of the array. Note that it is not that `select:select` purposefully copies over the elements; the case is that given the way Common Lisp arrays are, it actually is not possible to implement the functionality of `select:select` efficiently. For instance, [numcl:aref](https://numcl.github.io/numcl/#src/2aref.lisp) provides a similar functionality as `select:select`, however it too is slow in the general case:

```lisp
CL-USER> (let ((a (numcl:zeros '(1000 1000) :type t)))
           ;; select the ith element of all the rows, aka select the ith column
           (time (loop for i below 1000 do (numcl:aref a t i))))
Evaluation took:
  7.287 seconds of real time
  7.290938 seconds of total run time (7.290938 user, 0.000000 system)
  [ Run times consist of 0.051 seconds GC time, and 7.240 seconds non-GC time. ]
  100.05% CPU
  16,097,057,600 processor cycles
  1,752,658,096 bytes consed

NIL
CL-USER> (let ((a (numcl:zeros '(1000 1000) :type t)))
           ;; select the ith element of all the columns, aka select the ith row
           (time (loop for i below 1000 do (numcl:aref a i t))))
Evaluation took:
  0.000 seconds of real time
  0.001688 seconds of total run time (0.001618 user, 0.000070 system)
  100.00% CPU
  3,718,742 processor cycles
  457,408 bytes consed

NIL
```

The second case is faster here, because [cl:make-array](http://www.lispworks.com/documentation/HyperSpec/Body/f_mk_ar.htm) does provide `displaced-to` and `displaced-index-offset` which enable the creation of "wrapper" non-simple arrays if the elements are contiguous. This works only for the rightmost dimension, which is why in `(numcl:aref a i t)` with `t` being the rightmost index indicating "select all the elements along this axis", there is a way to avoid copying the array elements, but `(numcl:aref a t i)` necessitates copying.

By contrast, `dense-arrays::dense-array` provide for multidimensional strides and offsets, enabling copy free slicing for arbitrary axes. This enables the interpretation of the same `cl:vector` (the `storage` slot below) in multiple ways:

```lisp
DENSE-ARRAYS> (make-array '(2 3) :constructor #'+)
#<STANDARD-DENSE-ARRAY 2x3 :ROW-MAJOR T
   (0 1 2)
   (1 2 3)
 {100330F653}>
DENSE-ARRAYS> (describe *)
#<STANDARD-DENSE-ARRAY 2x3 :ROW-MAJOR T {100330F653}>
  [standard-object]

Slots with :INSTANCE allocation:
  STORAGE                        = #(0 1 2 1 2 3)
  DIMENSIONS                     = (2 3)
  ELEMENT-TYPE                   = T
  RANK                           = 2
  TOTAL-SIZE                     = 6
  STRIDES                        = (3 1)
  OFFSETS                        = (0 0)
  LAYOUT                         = :ROW-MAJOR
  ROOT-ARRAY                     = NIL
; No value
DENSE-ARRAYS> (aref (make-array '(2 3) :constructor #'+) 1)
#<STANDARD-DENSE-ARRAY NIL 3 T
   1
   2
   3
 {10033B1543}>
DENSE-ARRAYS> (aref (make-array '(2 3) :constructor #'+) nil 1)
#<STANDARD-DENSE-ARRAY NIL 2 T
   1
   2
 {10033B23E3}>
DENSE-ARRAYS> (describe *)
#<STANDARD-DENSE-ARRAY NIL 2 T  {10033B23E3}>
  [standard-object]

Slots with :INSTANCE allocation:
  STORAGE                        = #(0 1 2 1 2 3)
  DIMENSIONS                     = (2)
  ELEMENT-TYPE                   = T
  RANK                           = 1
  TOTAL-SIZE                     = 2
  STRIDES                        = (3)
  OFFSETS                        = (1)
  CONTIGUOUS-P                   = NIL
  ROOT-ARRAY                     = #<STANDARD-DENSE-ARRAY 2x3 T  {10033B2183}>
; No value
```

Indeed this also means that the arrays might not be contiguous. Contiguous dense arrays are of type `dense-arrays:simple-array`; for such arrays, `(array-layout dense-array)` returns either `:row-major` or `:column-major` which is also seen in the printed representations above. Otherwise if `(array-layout dense-array)` returns `nil`, then there is no guarantee that the array is a contiguous array, and one can use `dense-arrays:make-array` or `dense-arrays:copy-array` to obtain a `dense-arrays:simple-array` aka contiguous array.

Dense arrays provides two major types `(dense-arrays:array &optional element-type rank/dimensions)` and `(dense-arrays:simple-array &optional element-type rank/dimensions)` analogous to their CL counterparts. These should work with `typep` and `subtypep`, a caveat being that without the use of [extensible-compound-types](https://github.com/digikar99/extensible-compound-types), the dimensions are reduced to ranks. Thus, 

```lisp
CL-USER> (alexandria:type= '(dense-arrays:array * (2 3 4)) ; based on CL:TYPEP
                           '(dense-arrays:array * 3))
T
T
CL-USER> (extensible-compound-types:type= '(dense-arrays:array * (2 3 4))
                                          '(dense-arrays:array * 3))
NIL
T
```

For advanced usage relating to customizing the behavior of dense-arrays, see [here](https://github.com/digikar99/dense-arrays/blob/main/DOC.org). For a demonstration on optimizing dense-arrays, see [here](https://github.com/digikar99/dense-arrays/blob/main/perf.org) (you just need to use `dense-arrays:do-arrays` and declare the types!).

*Numpy arrays with their multidimensional strides and offsets might not be the best way to do things either. See [here](https://www.reddit.com/r/lisp/comments/wei81o/comment/iitttgb/?utm_source=share&utm_medium=web2x&context=3) for a discussion.


### Using numericals to extend numericals: implementing `mean`

As I write this, numericals already has the following operators: `sum divide expt multiply` operation.

In this section, we implement the `mean` operator as a simple wrapper around the existing operators. We start with a basic runtime correct function, but also go into compile time notes and optimizations.

#### Getting the correct answer at runtime

We start with `mean`, and take an inspiration from [numpy](https://numpy.org/doc/stable/reference/generated/numpy.mean.html) to formulate the function's parameter list:

```lisp
(numericals.common:compiler-in-package numericals.common:*compiler-package*)

(define-polymorphic-function nu:mean (array-like &key out axes keep-dims) :overwrite t)
```

We use `array-like` here, since from the user's perspective it will be convenient to avoid having to explicitly cast it into an array. Thus, immediately at the outset, we have two cases:

```lisp
(defpolymorph nu:mean ((array-like list) &key out axes keep-dims) t
  ...)
(defpolymorph nu:mean ((array-like nu:array) &key out axes keep-dims) t
  ...)
```

The subtype polymorphism provided by polymorphic-functions should help with the optimization: we should only be required to define a few cases for the `nu:array`, without bothering about each array-element-type. For the `list` case, the simplest definition would be:

```lisp
(defpolymorph nu:mean ((array-like list) &key out axes keep-dims) t
  (nu:mean (nu:asarray array-like) :out out :axes axes :keep-dims keep-dims))
```

Since a runtime array allocation is involved, we include a `suboptimal-note` in our definition, so that the user can be helped later. In addition, we also know that inlining this function won't cost much in terms of code bloat and there is a guarantee of non-recursion, thus we also provide an `:inline t` option to override polymorphic function's note about not inlining it.

```lisp
(defpolymorph (nu:mean :inline t :suboptimal-note runtime-array-allocation)
    ((array-like list) &key out axes) t
  (nu:mean (nu:asarray array-like) :out out :axes axes))
```

We will return to this case later. But now, let's start with the polymorph for `nu:array`. The plan is to calculate the `sum` and then divide by the `number-of-elements`:

```lisp
(defpolymorph nu:mean ((array-like nu:array) &key out axes keep-dims) t
  (let* ((sum (nu:sum array :out out :axes axes :keep-dims keep-dims))
         (number-of-elements ...))
    (nu:divide sum number-of-elements :out out)))
```

Wherever possible, we supply the `out` argument. `sum` could either be an array, or it could be a real. To calculate the `number-of-elements`, we consider three cases: `axes` is NIL, or an integer, or a list of integers.

```lisp
(defpolymorph nu:mean ((array-like nu:array) &key out axes keep-dims) t
  (let* ((sum (nu:sum array-like :out out :axes axes :keep-dims keep-dims))
         (number-of-elements
           (etypecase axes
             (null (array-total-size array-like))
             (integer (nu:shape array-like axes))
             (list (loop :for d :in (narray-dimensions array-like)
                         :for i :from 0
                         :with number-of-elements := 1
                         :if (member i axes)
                           :do (setf number-of-elements (* number-of-elements d))
                         :finally (return number-of-elements))))))
    (declare (type real number-of-elements))
    (nu:divide sum number-of-elements :out out)))
```

These two polymorphs seem enough to get us the required functionality at runtime. We can check out a few cases to see if the function is working correctly. Ideally, writing a test case would be recommendable.

```lisp
IMPL> (numericals.common:compiler-in-package numericals.common:*compiler-package*)
#<PACKAGE "DENSE-NUMERICALS.IMPL">
IMPL> (setq nu:*array-element-type* 'single-float)
SINGLE-FLOAT
IMPL> (nu:mean '(1 2 3))
2.0
IMPL> (nu:mean '(1 2 3.5))
2.1666667
IMPL> (nu:mean '((1 2 3) (4 5 6)))
3.5
IMPL> (nu:mean '((1 2 3) (4 5 6)) :axes 0)
#<STANDARD-DENSE-ARRAY :ROW-MAJOR 3 SINGLE-FLOAT
    2.500
    3.500
    4.500
 {10596A0B33}>
IMPL> (nu:mean '((1 2 3) (4 5 6)) :axes 1)
#<STANDARD-DENSE-ARRAY :ROW-MAJOR 2 SINGLE-FLOAT
    2.000
    5.000
 {10596A47C3}>
IMPL> (nu:mean '((1 2 3) (4 5 6)) :axes 1 :keep-dims t)
#<STANDARD-DENSE-ARRAY :ROW-MAJOR 2x1 SINGLE-FLOAT
  (  2.000    )
  (  5.000    )
 {102447F653}>
IMPL> (nu:mean '((1 2 3) (4 5 6)) :keep-dims t)
#<STANDARD-DENSE-ARRAY :ROW-MAJOR 1x1 SINGLE-FLOAT
  (  3.500    )
 {102448B023}>
```

Indeed, these seem good enough. Now let's turn to optimization and compiler notes!

#### Optimizing a specific case of nu:mean

For this part, it is helpful to use ALIVE or SLIME or equivalent, so that you can *compile* one form at a time and see the notes and warnings corresponding to it. We will be trying to optimize a simpler single-float-mean function that calculates the overall mean of single float arrays.


For our case, we note that one of the conditions which the `mean` function should satisfy includes that no runtime array allocation is involved. For the other conditions, let's use the compiler notes emitted by polymorphic-functions to discover what needs to be optimized!

If you are using SBCL (see `(cl:lisp-implementation-type)`, you might also want to declaim the following to focus on the compiler notes emitted by polymorphic-functions alone.

```lisp
(declaim (sb-ext:muffle-conditions sb-ext:compiler-note))
;; You can revert it with
;; (declaim (sb-ext:unmuffle-conditions sb-ext:compiler-note))
```

Now, compiling the previous `nu:mean` polymorph with `(optimize speed)` emits some notes:

```lisp
(defpolymorph nu:mean ((array-like nu:array) &key out axes keep-dims) t
  (declare (optimize speed))
  (let* ((sum (nu:sum array-like :out out :axes axes :keep-dims keep-dims))
         (number-of-elements
           (etypecase axes
             (null (array-total-size array-like))
             (integer (nu:shape array-like axes))
             (list (loop :for d :in (narray-dimensions array-like)
                         :for i :from 0
                         :with number-of-elements := 1
                         :if (member i axes)
                           :do (setf number-of-elements (* number-of-elements d))
                         :finally (return number-of-elements))))))
    (declare (type real number-of-elements))
    (nu:divide sum number-of-elements :out out)))
```

These notes mainly concern `nu:sum` and `nu:divide` and are about the type of `out` being not known. If you are using SLIME (or ALIVE?) you should see the `(nu:sum array-like :out out :axes axes :keep-dims keep-dims)` and `(nu:divide sum number-of-elements :out out)` forms being underlined to indicate where the notes are being emitted from.

What do we know about `out`? That, if it is supplied and if it has to be supplied, then it must be a `nu:array`. To keep things simple, we check this using whether or not the value of `out` is of type `null`. This naive way makes our function looks like this:

```lisp
(defpolymorph nu:mean ((array-like nu:array) &key out axes keep-dims) t
  (declare (optimize speed))
  (if (null out)
      (locally (declare (type null out))
        (let* ((sum (nu:sum array-like :out out :axes axes :keep-dims keep-dims))
               (number-of-elements
                 (etypecase axes
                   (null (array-total-size array-like))
                   (integer (nu:shape array-like axes))
                   (list (loop :for d :in (narray-dimensions array-like)
                               :for i :from 0
                               :with number-of-elements := 1
                               :if (member i axes)
                                 :do (setf number-of-elements (* number-of-elements d))
                               :finally (return number-of-elements))))))
          (declare (type real number-of-elements))
          (nu:divide sum number-of-elements :out out)))
      (locally (declare (type nu:array out))
        (let* ((sum (nu:sum array-like :out out :axes axes :keep-dims keep-dims))
               (number-of-elements
                 (etypecase axes
                   (null (array-total-size array-like))
                   (integer (nu:shape array-like axes))
                   (list (loop :for d :in (narray-dimensions array-like)
                               :for i :from 0
                               :with number-of-elements := 1
                               :if (member i axes)
                                 :do (setf number-of-elements (* number-of-elements d))
                               :finally (return number-of-elements))))))
          (declare (type real number-of-elements))
          (nu:divide sum number-of-elements :out out)))))
```

And compiling this gets us quite a few notes. Let's focus on one of them:

```
; (Compiler) Macro of
;    #<POLYMORPHIC-FUNCTIONS:POLYMORPHIC-FUNCTION TWO-ARG-FN/NON-COMPARISON (13)>
; is unable to optimize
;   (TWO-ARG-FN/NON-COMPARISON 'NU:DIVIDE X Y :BROADCAST BROADCAST)
; in
;   (LAMBDA (X Y &KEY (BROADCAST NU:*BROADCAST-AUTOMATICALLY*) (OUT NIL) &AUX)
;     (DECLARE (TYPE NULL OUT)
;              (TYPE T BROADCAST)
;              (TYPE REAL Y)
;              (TYPE T X)
;              (IGNORABLE))
;     (DECLARE (IGNORABLE OUT))
;     (BLOCK NU:DIVIDE
;       (LOCALLY
;         (TWO-ARG-FN/NON-COMPARISON 'NU:DIVIDE X Y :BROADCAST BROADCAST))))
; generated from
;   (NU:DIVIDE SUM NUMBER-OF-ELEMENTS :OUT OUT)
; because:
;
;   Type of
;     X
;   could not be determined
;   Type of
;     BROADCAST
;   could not be determined
```

From this, we can tell that `x` corresponds to `sum` and that nowhere have we told about the type of `sum`. In fact, the type of `sum`: it could be a `real` or it could be a `nu:array`. And in the case it is of type `nu:array`, we must necessarily broadcast. Thus, we have the following new definition:

```lisp
(defpolymorph nu:mean ((array-like nu:array) &key out axes keep-dims) t
  (declare (optimize speed))
  (if (null out)
      (locally (declare (type null out))
        (let* ((sum (nu:sum array-like :out out :axes axes :keep-dims keep-dims))
               (number-of-elements
                 (etypecase axes
                   (null (array-total-size array-like))
                   (integer (nu:shape array-like axes))
                   (list (loop :for d :in (narray-dimensions array-like)
                               :for i :from 0
                               :with number-of-elements := 1
                               :if (member i axes)
                                 :do (setf number-of-elements (* number-of-elements d))
                               :finally (return number-of-elements))))))
          (declare (type real number-of-elements))
          (if (realp sum)
              (locally (declare (type real sum))
                (nu:divide sum number-of-elements))
              (locally (declare (type nu:array sum))
                (nu:divide sum number-of-elements :out out :broadcast t)))))
      (locally (declare (type nu:array out))
        (let* ((sum (nu:sum array-like :out out :axes axes :keep-dims keep-dims))
               (number-of-elements
                 (etypecase axes
                   (null (array-total-size array-like))
                   (integer (nu:shape array-like axes))
                   (list (loop :for d :in (narray-dimensions array-like)
                               :for i :from 0
                               :with number-of-elements := 1
                               :if (member i axes)
                                 :do (setf number-of-elements (* number-of-elements d))
                               :finally (return number-of-elements))))))
          (declare (type real number-of-elements))
          (if (realp sum)
              (locally (declare (type real sum))
                (nu:divide sum number-of-elements))
              (locally (declare (type nu:array sum))
                (nu:divide sum number-of-elements :out out :broadcast t)))))))
```

Compiling this gets us some other notes now:

```
; While compiling
;     (TWO-ARG-FN/NON-COMPARISON 'NU:DIVIDE X Y :BROADCAST BROADCAST)
; in
;   (LAMBDA (X Y &KEY (BROADCAST NU:*BROADCAST-AUTOMATICALLY*) (OUT NIL) &AUX)
;     (DECLARE (TYPE NULL OUT)
;              (TYPE (EQL T) BROADCAST)
;              (TYPE REAL Y)
;              (TYPE ARRAY X)
;              (IGNORABLE))
;     (DECLARE (IGNORABLE OUT))
;     (BLOCK NU:DIVIDE
;       (LOCALLY
;         (TWO-ARG-FN/NON-COMPARISON 'NU:DIVIDE X Y :BROADCAST BROADCAST))))
; generated from
;   (NU:DIVIDE SUM NUMBER-OF-ELEMENTS :OUT OUT :BROADCAST T)
;   Following notes were encountered:
;
;     No applicable POLYMORPH discovered for polymorphic-function
;       TWO-ARG-FN/NON-COMPARISON
;     and ARGS:
;
;       ('NU:DIVIDE X Y :BROADCAST BROADCAST)
;
;     derived to be of TYPES:
;
;       ((EQL NU:DIVIDE) ARRAY REAL (EQL :BROADCAST) (EQL T))
;
;     Available Effective-Type-Lists include:
;
;       (SYMBOL NUMBER NUMBER &KEY (:BROADCAST T) (:OUT NULL))
;       (SYMBOL LIST LIST &KEY (:BROADCAST (OR NULL T)) (:OUT ARRAY))
;       (SYMBOL NUMBER LIST &KEY (:BROADCAST (OR NULL T)) (:OUT ARRAY))
;       (SYMBOL LIST NUMBER &KEY (:BROADCAST (OR NULL T)) (:OUT ARRAY))
;       (SYMBOL LIST LIST &KEY (:BROADCAST (OR NULL T)) (:OUT NULL))
;       (SYMBOL NUMBER LIST &KEY (:BROADCAST (OR NULL T)) (:OUT NULL))
;       (SYMBOL LIST NUMBER &KEY (:BROADCAST (OR NULL T)) (:OUT NULL))
;       (SYMBOL (SIMPLE-ARRAY <TYPE>) (SIMPLE-ARRAY <TYPE>) &KEY
;        (:BROADCAST (OR NULL NULL)) (:OUT (SIMPLE-ARRAY <TYPE>)))
;       (SYMBOL (SIMPLE-ARRAY <TYPE>) (SIMPLE-ARRAY <TYPE>) &KEY
;        (:BROADCAST (OR NULL NULL)) (:OUT NULL))
;       (SYMBOL (ARRAY <TYPE>) (ARRAY <TYPE>) &KEY (:BROADCAST (OR NULL T))
;        (:OUT (ARRAY <TYPE>)))
;       (SYMBOL (ARRAY <TYPE>) (ARRAY <TYPE>) &KEY (:BROADCAST (OR NULL T))
;        (:OUT NULL))
;       (SYMBOL (ARRAY <TYPE>) NUMBER &KEY (:BROADCAST (OR NULL (NOT NULL)))
;        (:OUT (OR NULL (ARRAY <TYPE>))))
;       (SYMBOL NUMBER (ARRAY <TYPE>) &KEY (:BROADCAST (OR NULL (NOT NULL)))
;        (:OUT (OR NULL (ARRAY <TYPE>))))
```

No applicable polymorph for `divide`? How come? Certainly there exists a polymorph for `divide` that can take in a `nu:array` and a `real` as input, right? Careful observation tells us that even though such a polymorph exists (see the last two type lists), such a polymorph also has constraints on the type of `out`. In particular, the element-type of `out` must match the element type of `x` or `y`, or in our case, the `sum`. In fact, the element-type of `sum` if it happens to be an `nu:array` is also determined by the element-type of the input `array-like`.


1. **divide:** We know that the second argument to `divide` will always be a `real`, and as such broadcasting cannot be avoided. However, `divide` accepts an `out` argument, and we would like to avoid runtime allocation of `out`. But, while supplying `out` we would want to check if or not `sum` is a real, because if `sum` is a real, then both `sum` and `number-of-elements` are real, and then we do not need to supply `out`. The following is our modified function then. Let's try to compile with `(optimize speed)` to see what notes are emitted. 

```lisp
(defpolymorph nu:mean ((array-like nu:array) &key out axes keep-dims) t
  (let* ((sum (nu:sum array-like :out out :axes axes :keep-dims keep-dims))
         (number-of-elements
           (etypecase axes
             (null (array-total-size array-like))
             (integer (nu:shape array-like axes))
             (list (loop :for d :in (narray-dimensions array-like)
                         :for i :from 0
                         :with number-of-elements := 1
                         :if (member i axes)
                           :do (setf number-of-elements (* number-of-elements d))
                         :finally (return number-of-elements))))))
    (declare (type real number-of-elements))
    (if (realp sum)
        (locally (declare (type real sum))
          (nu:divide sum number-of-elements))
        (locally (declare (type nu:array sum))
          (nu:divide sum number-of-elements :out out :broadcast t)))))
```


```lisp
(defun mean-caller (x)
  (declare (optimize speed))
  (nu:mean x))
```

The crucial note this emits is:

```
    Type of
      X
    could not be determined
```

So, we specify the type of `x`:

```
(defun mean-caller (x)
  (declare (optimize speed)
           (type (simple-array single-float) x))
  (nu:mean x))
```

This gets us more information.


```lisp
(defpolymorph nu:mean ((array-like nu:array) &key out axes keep-dims) t
  (let* ((sum (nu:sum array-like :out out :axes axes :keep-dims keep-dims))
         (number-of-elements
           (etypecase axes
             (null (array-total-size array-like))
             (integer (nu:shape array-like axes))
             (list (loop :for d :in (narray-dimensions array-like)
                         :for i :from 0
                         :with number-of-elements := 1
                         :if (member i axes)
                           :do (setf number-of-elements (* number-of-elements d))
                         :finally (return number-of-elements))))))
    (declare (type real number-of-elements))
    (nu:divide sum number-of-elements :out out)))
```



### Interfacing with magicl
